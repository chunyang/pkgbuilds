# Contributor: Chun Yang <x@cyang.info>
pkgname=intel-vtune-amplifier-xe
pkgver=2013update5
pkgrel=3
pkgdesc="Easy to use performance and thread profiler for C, C++, C#, Fortran, Java and MPI developers."
arch=('i686' 'x86_64')
url=http://software.intel.com/en-us/non-commercial-software-development
license=('custom')
depends=('libsm' 'pangox-compat' 'gtk2' 'java-environment' 'intel-tbb')
#depends=('libpng12' 'libjpeg6')
makedepends=('rpmextract' 'linux-headers' 'unzip')
install=intel-vtune-amplifier-xe.install
source=('http://registrationcenter-download.intel.com/akdlm/irc_nas/3020/vtune_amplifier_xe_2013_update5.tar.gz'
        'cpuevents-patch.diff'
        'intel-vtune.sh'
        'intel-vtune.service')
md5sums=('756b277b3baba85f788473b137c82bda'
         '1601383fa16adbbf0e79b6a1c9fab855'
         '5f96a40d30795b8b35cf2e2b00684122'
         'bdbefe40842e0bd811b57f2528d728e9')

build() {
    cd "$srcdir/vtune_amplifier_xe_2013_update5/"

    find -name "*.rpm" -exec rpmextract.sh {} \;

    # bulid sep drivers
    pushd opt/intel/vtune_amplifier_xe_2013/sepdk/src
    patch -uN vtsspp/cpuevents.c "$srcdir/cpuevents-patch.diff"
    ./build-driver -ni
    popd

    # build power drivers
    pushd opt/intel/vtune_amplifier_xe_2013/powerdk/src
    ./build-driver -ni
    popd

    # build performance counter monitor
    pushd opt/intel/vtune_amplifier_xe_2013/contrib
    unzip intelperformancecountermonitorv2.35.zip
    cd IntelPerformanceCounterMonitor-V2.35
    make
    popd
}

package() {
    cd "$srcdir/vtune_amplifier_xe_2013_update5/"

    if [ "$CARCH" == "x86_64" ]; then
        _carch=64
        _carch2=x32_64
    elif [ "$CARCH" == "i686" ]; then
        _carch=32
        _carch2=x32
    else
        echo "Unknown CARCH $CARCH"
        exit 1
    fi

    cp -aR opt "$pkgdir"

    # License
    install -D -m644 EULA.txt "$pkgdir/usr/share/licenses/$pkgname/EULA.txt"

    # Startup scripts
    install -D -m755 "$srcdir/intel-vtune.sh" "$pkgdir/etc/profile.d/intel-vtune.sh"
    install -D -m644 "$srcdir/intel-vtune.service" "$pkgdir/usr/lib/systemd/system/intel-vtune.service"

    # Modules
    install -D -m644 opt/intel/vtune_amplifier_xe_2013/sepdk/src/pax/pax.ko "$pkgdir/usr/lib/modules/$(uname -r)/extramodules/pax.ko"
    install -D -m644 opt/intel/vtune_amplifier_xe_2013/sepdk/src/vtsspp/vtsspp.ko "$pkgdir/usr/lib/modules/$(uname -r)/extramodules/vtsspp.ko"
    install -D -m644 opt/intel/vtune_amplifier_xe_2013/sepdk/src/sep3_10.ko "$pkgdir/usr/lib/modules/$(uname -r)/extramodules/sep3_10.ko"
    install -D -m644 opt/intel/vtune_amplifier_xe_2013/powerdk/src/apwr3_1.ko "$pkgdir/usr/lib/modules/$(uname -r)/extramodules/apwr3_1.ko"

    # Scripts
    cp -a opt/intel/vtune_amplifier_xe_2013/sepdk/src/pax/insmod-pax "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/insmod-pax"
    cp -a opt/intel/vtune_amplifier_xe_2013/sepdk/src/vtsspp/insmod-vtsspp "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/insmod-vtsspp"
    cp -a opt/intel/vtune_amplifier_xe_2013/sepdk/src/insmod-sep3 "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/insmod-sep3"
    cp -a opt/intel/vtune_amplifier_xe_2013/powerdk/src/insmod-apwr "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/insmod-apwr"
    cp -a opt/intel/vtune_amplifier_xe_2013/sepdk/src/pax/rmmod-pax "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/rmmod-pax"
    cp -a opt/intel/vtune_amplifier_xe_2013/sepdk/src/vtsspp/rmmod-vtsspp "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/rmmod-vtsspp"
    cp -a opt/intel/vtune_amplifier_xe_2013/sepdk/src/rmmod-sep3 "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/rmmod-sep3"
    cp -a opt/intel/vtune_amplifier_xe_2013/powerdk/src/rmmod-apwr "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/rmmod-apwr"

    cp -a opt/intel/vtune_amplifier_xe_2013/sepdk/src/update_sep_version.pl "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/update_sep_version.pl"

    ln -s . "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/pax"
    ln -s . "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/vtsspp"

    cp -a opt/intel/vtune_amplifier_xe_2013/sepdk/src/boot-script "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/boot-script-sep3"
    cp -a opt/intel/vtune_amplifier_xe_2013/powerdk/src/boot-script "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/boot-script-apwr"

    ln -s /usr/lib/modules/$(uname -r)/extramodules/pax.ko "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/pax-$_carch2-$(uname -r)smp.ko"
    ln -s /usr/lib/modules/$(uname -r)/extramodules/vtsspp.ko "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/vtsspp-$_carch2-$(uname -r)smp.ko"
    ln -s /usr/lib/modules/$(uname -r)/extramodules/sep3_10.ko "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/sep3_10-$_carch2-$(uname -r)smp.ko"
    ln -s /usr/lib/modules/$(uname -r)/extramodules/apwr3_1.ko "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/apwr3_1-$_carch2-$(uname -r)smp.ko"

    # Contrib binaries
    install -D -m755 opt/intel/vtune_amplifier_xe_2013/contrib/IntelPerformanceCounterMonitor-V2.35/*.x "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/"

    cd "$pkgdir/opt/intel/vtune_amplifier_xe_2013"

    # fix path bug in insmod-apwr
    sed -i '/^DRIVER_FILENAME=/d;s/^#\(DRIVER_FILENAME=\)/\1/' "$pkgdir/opt/intel/vtune_amplifier_xe_2013/bin$_carch/insmod-apwr"

    # generate amplxe-vars.sh and amplxe-vars.csh using correct paths
    sh <(sed "s|^\(SCRIPT_DIR=\).*|\1/opt/intel/vtune_amplifier_xe_2013|;
              s|^\(OUTPUT_[^_]*_PATH=\)|\1${pkgdir}/|" amplxe-genvars.sh)

    # Remove everything that is not needed
    rm -rf sdk mic_sepdk sepdk powerdk contrib documentation
    if [ "$CARCH" == "x86_64" ]; then
        rm -rf bin32 include/ia32 lib32 target/linux32 target/linux64/mic_sepdk
        #find lib64 ! -name '*amplxe*' -a -name '*.so*' -exec rm {} \;
    elif [ "$CARCH" == "i686" ]; then
        rm -rf bin64 include/intel64 lib64 target/linux64
        #find lib32 ! -name '*amplxe*' -a -name '*.so*' -exec rm {} \;
    else
        echo "Unknown CARCH $CARCH"
        exit 1
    fi
}
