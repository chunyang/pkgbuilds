# Maintainer: Chun Yang <x@cyang.info>
pkgname=ghost
pkgver=0.5.0
pkgrel=1
pkgdesc="Free, open, simple blogging platform"
arch=('i686' 'x86_64')
url="http://ghost.org"
license=('MIT')
depends=('nodejs>0.10' 'nodejs<0.11.0')
backup=('srv/ghost/Gruntfile.js' 'srv/ghost/index.js' 'srv/ghost/package.json')
install=ghost.install
source=(http://ghost.org/zip/$pkgname-$pkgver.zip
        ghost.service)
sha256sums=('3901fd0efe0a0ea40c433e670f0b41ef25dabef41c9e3b3d6f3cf9716e416459'
            'c454a46382a56846115169788af1a70e80941cc4ef483d9661008cfcf2cfc123')

# Note: You may need to log into ghost.org and download the zip file manually
# and place it inside the same directory as the PKGBUILD

package() {
    install -dm755 "$pkgdir/srv/ghost"
    cd "$pkgdir/srv/ghost"

    install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/ghost/LICENSE"

    install -Dm644 "$srcdir/ghost.service" "${pkgdir}/usr/lib/systemd/system/ghost.service"

    bsdtar -xf "$srcdir/$pkgname-$pkgver.zip"
    npm install --production

    chmod -R g=u "$pkgdir/srv/ghost"
    find "$pkgdir/srv/ghost" -type d -exec chmod g+s {} \;

    cat <<-EOF

	Upgrading Ghost involves replacing old files with the new files, and
	restarting the server. However, as the database, image uploads and
	custom themes are stored alongside Ghost in the content directory,
	care should be taken to only replace the necessary files as explained at
	http://support.ghost.org/how-to-upgrade/ .

	It is highly recommended that you make a backup of your data before
	upgrading. To backup all the data from your database, log into your
	Ghost install and go to /ghost/debug/. Press the export button to
	download a JSON file containing all of your data.

	EOF
}
